#!/bin/env guile3.0
!#
(import (thinker app) (ice-9 match) (ice-9 readline))

(define current-dir (dirname (current-filename)))

;; The extension loader
(define load-ext
    (case-lambda
        ((mod-path)
         ((module-ref (resolve-module mod-path) 'install)))
        ((mod-path version)
         ((module-ref (resolve-module mod-path #t version) 'install)))))

;; Test the extension loader
(load-ext '(thinker example connectives) '(1 0))

(define thinker (make-thinker))

;; Enable readline for the current input port when it is a terminal
(activate-readline)

(let loop ()
    (define r (read))
    (if (eof-object? r)
        #f
        (match r
            (('save (? string? path))
             (call-with-output-file path (lambda (out) (map (lambda (f) (write f out)) (thinker 'list))))
             (loop))
            (('load (? string? path))
             (call-with-input-file path (lambda (in) (let loop () (let ((f (read in))) (if (eof-object? f) #f (begin (thinker 'add f) (loop)))))))
             (loop))
            (('load-ext path)
             (load-ext path)
             (loop))
            (('load-ext path vers)
             (load-ext path vers)
             (loop))
            (('exit) #f)
            (('commands)
             (map (lambda (d) (display d) (newline))
                  '((save "<path>")
                    (load "<path>")
                    (load-script "<path>")
                    (exit)
                    (commands)
                    ;; Supported by app.scm
                    (add "<form>")
                    (remove "<index>")
                    (list)
                    (list-prims)
                    (list-conns)
                    (entails? "<form>")
                    (clear)))
             (loop))
            (v
             (let ((r (apply thinker v)))
                (unless (unspecified? r)
                    (write r) (newline))
                (loop))))))