(library (data expand)
    (export expand-proposition expandable-form? install-connective!)
    (import (data parse) (algo pkg) (algo list) (scheme base) (srfi srfi-69) (srfi srfi-28) (exn contract) (exn internal) (ice-9 match))

    (define-values (install-connective! get-connective has-connective?) (make-pkg-manager))
    
    (define (expandable-form? f)
        (and (list? f)
             (pair? f)
             (symbol? (car f))))
    (define (expand-proposition f)
        (let loop ((f f))
            (match f
                (((and (or 'and 'or) conn) cs ...)
                 (cons conn (map loop cs)))
                (('not c)
                 (list 'not (loop c)))
                ((? string? v)
                 v)
                (((? symbol? conn) cs ...)
                 (unless (has-connective? conn)
                    (raise-internal-error 'expand-proposition (format "connective ~s not found" conn)))
                 (define r (apply (get-connective conn) (map loop cs)))
                 (unless (proposition-representation? r)
                    (raise-contract-error conn "proposition-representation?" r))
                 r)
                (form (raise-internal-error 'expand-proposition (format "malformed proposition ~s" form)))))))