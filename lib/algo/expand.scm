(library (algo expand)
    (export expand-proposition)
    (import (algo parse) (rnrs (6)) (srfi srfi-69) (srfi srfi-28) (exn contract) (exn internal) (ice-9 match))
    
    (define (expand-proposition f e)
        (unless (hash-table? e)
            (raise-contract-error 'expand-proposition "hash-table?" e))
        (hash-table-walk e 
            (lambda (k _)
                (unless (symbol? k) 
                    (raise-contract-error 'expand-proposition "symbol?" k))))
        (let loop ((f f))
            (match f
                (((and (or 'and 'or) conn) cs ...)
                 (cons conn (map loop cs)))
                (('not c)
                 (list 'not (loop c)))
                ((? string? v)
                 v)
                (((? symbol? conn) cs ...)
                 (unless (hash-table-exists? e conn)
                    (raise-internal-error 'expand-proposition (format "Connective ~s not found" conn)))
                 (define r (apply (hash-table-ref e conn) (map loop cs)))
                 (unless (proposition-representation? r)
                    (raise-contract-error conn "proposition-representation?" r))
                 r)
                (form (raise-internal-error 'expand-proposition (format "malformed proposition ~s" form)))))))