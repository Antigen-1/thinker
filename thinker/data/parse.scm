(library (thinker data parse)
    (export parse-proposition unparse-proposition proposition-representation?)
    (import (ice-9 match) 
            (thinker data connective) 
            (thinker data primitive)
            (thinker data node)
            (thinker algo list)
            (thinker exn contract)
            (rnrs (6)))
    
    (define (proposition-representation? t)
        (let loop ((t t))
            (match t
                ((? string? v) #t)
                (((or 'and 'or) cs ...)
                 (andmap loop cs))
                (('not c)
                 (loop c))
                (_ #f))))
    (define (unparse-proposition p)
        (unless (node? p)
            (raise-contract-error 'unparse-proposition "node?" p))
        (let loop ((p p))
            (cond ((and? p) (cons 'and (map loop (children p))))
                  ((or? p) (cons 'or (map loop (children p))))
                  ((not? p) (cons 'not (map loop (children p))))
                  (else (primitive-name p)))))
    (define (parse-proposition t)
        (let loop ((t t))
            (match t
                ((? string? v) (Prim v))
                (('and cs ...)
                 (apply & (map loop cs)))
                (('or cs ...)
                 (apply || (map loop cs)))
                (('not c)
                 (! (loop c)))
                (_ (raise-contract-error 'parse-proposition "proposition-representation?" t))))))